plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.android.zxkj.cast_screen"
        minSdkVersion rootProject.ext.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias "key0"
            keyPassword '123456'
            storeFile file('./keys/cast-screen.jks')
            storePassword '123456'
        }
        release {
            keyAlias "key0"
            keyPassword '123456'
            storeFile file('./keys/cast-screen.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (variant.buildType.name == 'release') {
                def createTime = new Date().format("YYYYMMddhhmm", TimeZone.getTimeZone("GMT+08:00"))
                def fileName = "Cast-Release-v${android.defaultConfig.versionName}-${createTime}.apk"
                output.outputFileName = new File(outputFile.parent, fileName).name
            } else {
                def fileName = "Cast-${variant.buildType.name}-v${android.defaultConfig.versionName}.apk"
                output.outputFileName = new File(outputFile.parent, fileName).name
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude "META-INF/DEPENDENCIES"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.appcompat:appcompat:$rootProject.ext.supportLibraryVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintlayoutVersion"
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"


    implementation 'androidx.recyclerview:recyclerview:1.1.0'


    implementation 'com.permissionx.guolindev:permissionx:1.4.0'

    implementation project(':dlna-dmc')
    implementation project(':dlna-dms')
}
